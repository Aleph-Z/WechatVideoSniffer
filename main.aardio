import win.ui;
import inet.http
import console
/*DSG{{*/
mainForm = win.form(text="WechatVideoSniffer";right=959;bottom=591)
mainForm.add(
btnClear={cls="button";text="清空列表";left=797;top=540;right=889;bottom=580;z=5};
btnCopyUrl={cls="button";text="复制链接";left=797;top=177;right=889;bottom=211;z=6};
btnDown={cls="button";text="下载选中";left=794;top=120;right=885;bottom=157;z=4};
btnSniffer={cls="button";text="监听";left=794;top=14;right=885;bottom=51;z=1};
btnStop={cls="button";text="停止";left=794;top=70;right=885;bottom=107;z=2};
listview={cls="listview";left=17;top=15;right=787;bottom=584;edge=1;hscroll=1;z=3}
)
/*}}*/

import dotNet
import win.clip
mainForm.listview.insertColumn( "URL",-1 )
dotNet.reference({
	"FiddlerCore4" = $"res\FiddlerCore4.dll";
	"CertMaker" = $"res\CertMaker.dll";
	"BCMakeCert" = $"res\BCMakeCert.dll";
	"BasicFormatsForCore" = $"res\BasicFormatsForCore.dll"
})

var FiddlerCore4 = dotNet.load("FiddlerCore4")
Fiddler = FiddlerCore4.import("Fiddler")

InstallCertificate = function(){
	if(!Fiddler.CertMaker.rootCertExists()){
		if(!Fiddler.CertMaker.createRootCert()){
			return false;
		}
		if(Fiddler.CertMaker.trustRootCert()){
			return false;
		}
	}
	return true;
}

UninstallCertificate = function(){
	if(Fiddler.CertMaker.rootCertExists()){
		if(!Fiddler.CertMaker.removeFiddlerGeneratedCerts(true)){
			return false;
		}
	}
	return true;
}

var http = inet.http()

// 视频链接过滤规则
var urls = {}

sendDownload = function(url){
	if (!urls[url]) {
	    urls[url] = true
	    mainForm.listview.addItem( {url} )
    try{
            http.post("http://127.0.0.1:3000/api", {url = url});
    }catch(e){

    }
  }
}

filterCanDownloadUrl = function(url){
	var result = !!(string.startWith(url,"https://voipfindervod2.wxqcloud.qq.com") and string.find(url,".mp4"))
	var result2 = !!(string.startWith(url,"https://pull-m1.wxlivecdn.com") and string.find(url,".flv"))
	
	return result or result2
}

filterUrl = function(url){
	if (filterCanDownloadUrl(url)){
		sendDownload(url)
		return true
	}
	if(string.find(url,"^https\:\/\/finder<mp>*\.video\.qq\.com\/251\/20302\/") or string.find(url,"mpvideo\.qpic\.cn") ){
	 	// 去除无用链接格式
	 	if(!!string.match(url,"token=\w+")){
	 		sendDownload(url)	
	 	}
	} else {
		var isNext = (string.find(url, "\.wxqcloud\.qq\.com") and string.find(url, "\.flv")) and string.find(url, "\/trtc_")
		var i1 = !string.find(url, "\.smtcdns\.net")
		var i2 = !string.find(url, "\.bytefcdnrd\.com\/trtc_")
		
		if (isNext2 or (isNext and i1 and i2)) {
			sendDownload(url)
		}
	}
	return false; 
}


AttachListening = function(){
	 Fiddler.FiddlerApplication.BeforeRequest = function(s){
	 	s.bBufferResponse = false;
	 	if(filterUrl(s.fullUrl)){
	 		mainForm.listview.addItem( {s.fullUrl} )
	 	}
	 }
/*
	 Fiddler.FiddlerApplication.BeforeResponse = function(s){
		if(string.startWith(s.fullUrl,"https://finder.video.qq.com")){
	 		mainForm.listview.addItem( {s.fullUrl} )
	 	}
	 }
*/
}

StartupFiddlerCore = function(){
	Fiddler.FiddlerApplication.Startup(9898,Fiddler.FiddlerCoreStartupFlags.Default | Fiddler.FiddlerCoreStartupFlags.RegisterAsSystemProxy | Fiddler.FiddlerCoreStartupFlags.AllowRemoteClients)
}

UninstallFiddler = function(){
	if(Fiddler.FiddlerApplication.IsStarted()){
		Fiddler.FiddlerApplication.Shutdown();
	}
}

mainForm.btnSniffer.oncommand = function(id,event){
	InstallCertificate()
	AttachListening()
	StartupFiddlerCore()
	mainForm.btnSniffer.disabled = true
}

mainForm.btnStop.oncommand = function(id,event){
	UninstallCertificate()
	UninstallFiddler()
	mainForm.btnSniffer.disabled = false
}

mainForm.btnClear.oncommand = function(id,event){
	mainForm.listview.clear()
	urls = {}
}

mainForm.btnDown.oncommand = function(id,event){
	var index = mainForm.listview.selIndex
	if(index>0){
		var url = mainForm.listview.getItemText(index,1,10240)
		mainForm.btnDown.text = "下载中"
		mainForm.btnDown.disabled = true
		thread.invokeAndWait(
			function(url){
				import inet.httpFile
				math.randomize()
				var remoteFile = inet.httpFile( url,"/download/" + string.random(6) + ".mp4" )
				remoteFile.test()
				remoteFile.download()
				remoteFile.close()
			},url
		)
		mainForm.msgbox("下载完成")
		mainForm.btnDown.text = "下载选中"
		mainForm.btnDown.disabled = false
	}
}

mainForm.onClose = function(hwnd,message,wParam,lParam){
   	//UninstallCertificate()
	UninstallFiddler() 
}

mainForm.btnCopyUrl.oncommand = function(id,event){
	var index = mainForm.listview.selIndex
	if(index>0){
		var url = mainForm.listview.getItemText(index,1,10240)
		win.clip.write(url)
	}
}

mainForm.show();
return win.loopMessage();
